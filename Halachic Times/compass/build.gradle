apply plugin: 'com.android.application'

def versionMajor = project.APP_VERSION_MAJOR
def versionMinor = project.APP_VERSION_MINOR.padLeft(2, '0')
def versionCodeValue = Integer.parseInt(project.APP_VERSION_CODE)

android {
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId "net.sf.times.compass"
        minSdkVersion Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
        versionCode versionCodeValue
        versionName versionMajor + '.' + versionMinor

        testApplicationId "net.sf.times.compass.test"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }

    signingConfigs {
        release {
            storeFile file("release.keystore")
            storePassword project.STORE_PASSWORD_RELEASE
            keyAlias "release"
            keyPassword project.KEY_PASSWORD_RELEASE
        }
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        disable "GoogleAppIndexingWarning"
        disable "LocaleFolder"
        disable "UnusedAttribute"
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':lib')
    compile project(':common')
    compile project(':locations')
    debugCompile project(path: ':compass-lib', configuration: 'debug')
    releaseCompile project(path: ':compass-lib', configuration: 'release')
}
