apply plugin: 'com.android.application'

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

static def joinStringArray(array) {
    return "{\"" + array.join("\", \"") + "\"}"
}

def versionMajor = Integer.parseInt(project.APP_VERSION_MAJOR)
def versionMinor = Integer.parseInt(project.APP_VERSION_MINOR)

android {
    compileSdkVersion androidBuildSdkVersion

    defaultConfig {
        applicationId "net.sf.times"
        minSdkVersion androidBuildMinSdkVersion
        targetSdkVersion androidBuildTargetSdkVersion
        versionCode versionMajor * 100 + versionMinor
        versionName versionMajor + '.' + versionMinor.toString().padLeft(2, '0')

        def locales = ["bg", "cs", "da", "de", "en", "es", "es_US", "et", "fi", "fr", "hu", "it", "iw", "lt", "nb", "nl", "no", "pl", "pt", "pt_PT", "ro", "ru", "sv", "uk"]
        resConfigs locales
        buildConfigField "String[]", "LOCALES", joinStringArray(locales)

        manifestPlaceholders = [offline: false]

        testApplicationId "net.sf.times.test"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("../release.keystore")
            storePassword project.STORE_PASSWORD_RELEASE
            keyAlias "release"
            keyPassword project.KEY_PASSWORD_RELEASE
        }
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
        }

        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "internet"
    productFlavors {
        normal {
            dimension "internet"
        }

        offline {
            dimension "internet"
            manifestPlaceholders = [offline: true]
        }
    }

    lintOptions {
        disable "GoogleAppIndexingWarning"
        //disable "IconLauncherFormat" // v26 has XML (non-PNG) adaptive icons.
        disable "InconsistentLayout"
        disable "LocaleFolder"
        disable "MergeRootFrame"
        disable "Overdraw"
        disable "PluralsCandidate"
        disable "UnusedAttribute"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':zmanim:lib-android')
    implementation project(':android-lib:lib')
    implementation project(':common')
    implementation project(':locations')
    implementation project(':compass-lib')

    // Testing
    testImplementation 'junit:junit:4.13'
    androidTestImplementation "androidx.test:core:$supportTestVersion"
    androidTestImplementation "androidx.test:rules:$supportTestVersion"
    androidTestImplementation "androidx.test:runner:$supportTestVersion"
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    implementation 'com.google.firebase:firebase-crashlytics:17.0.0'
}
